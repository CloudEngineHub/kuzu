# Error: Assertion failed in file "/Users/guodongjin/Developer/kuzu/src/common/types/types.cpp" on line 1805: KU_UNREACHABLE
-NAME IC3
-QUERY MATCH (countryX:Place{name: 'India' }),
             (countryY:Place{name: 'China' }),
             (person:Person {id: 933 })
       WITH person, countryX, countryY
       LIMIT 1
       MATCH (city:Place)-[:isPartOf]->(country:Place)
       WHERE city.type='city' AND country.type='country' AND country IN [countryX, countryY]
       WITH person, countryX, countryY, collect(city) AS cities
       MATCH (person)-[:KNOWS*1..2]-(friend)-[:personIsLocatedIn]->(city)
       WHERE NOT person=friend AND NOT city IN cities
       WITH DISTINCT friend, countryX, countryY
       MATCH (friend)<-[:messageHasCreator]-(message),
             (message)-[:personIsLocatedIn]->(country)
       WHERE '2020-12-30' > message.creationDate AND message.creationDate >= '2000-01-01' AND
             country IN [countryX, countryY]
       WITH friend,
            CASE WHEN country=countryX THEN 1 ELSE 0 END AS messageX,
            CASE WHEN country=countryY THEN 1 ELSE 0 END AS messageY
       WITH friend, sum(messageX) AS xCount, sum(messageY) AS yCount
       WHERE xCount>0 AND yCount>0
       RETURN friend.id AS friendId,
              friend.firstName AS friendFirstName,
              friend.lastName AS friendLastName,
              xCount,
              yCount,
              xCount + yCount AS xyCount
       ORDER BY xyCount DESC, friendId ASC
       LIMIT 20
---- 1
